#!/usr/bin/perl

use warnings;
use strict;
use utf8;

use AnyEvent;
use AnyEvent::Impl::Perl;
use AnyEvent::XMPP::Client;
use AnyEvent::HTTPD;

use Data::Dumper;
use Data::Uniqid qw(uniqid);
use Getopt::Std;
use POSIX;
use JSON;
use Carp;


#my @developers = ('migalin@welltime.ru', 'arbuzov@welltime.ru', 'xoicy@welltime.ru', 'yaroslav@welltime.ru');
my @developers = ('migalin@welltime.ru');
my $httpd;

my $j = AnyEvent->condvar;
my $cl = AnyEvent::XMPP::Client->new(debug => 0);
sub print_log
{
	return print(strftime('[%F %T] ', localtime()), @_);
}

sub daemonize                                                                       
{                                                                                   
	my ($log_filename, $pidfile) = @_;                                              
	chdir '/';                                                                      
	umask 0;                                                                        
	open(STDIN, '<', '/dev/null')   or croak("Cannot read /dev/null: $!");          
	if(defined $log_filename) {
		open(STDOUT, '>>', $log_filename) or croak("Cannot write to $log_filename: $!");
		open(STDERR, '>>', $log_filename) or croak("Cannot write to $log_filename: $!");
	}
	defined(my $pid = fork()) or croak("Cannot fork: $!");                          
	exit if $pid;                                                                   
	open( my $PIDFILE, '>', $pidfile) or croak("Cannot open pidfile $pidfile!\n");  
	print $PIDFILE $$ . "\n";                                              
	close($PIDFILE);                                                                
	setsid or croak "Can't start a new session: $!";                                
	$0 = 'github-notifier';                                                            
	print_log "Github notifier daemon successfully started with PID $$\n";    
	return 1;                                                                       
}                                                                                   

sub getDisplayName 
{
	my ($committer) = @_;
	return $committer->{'name'}	. ' <' . $committer->{'email'} . '>';
}

sub truncateSHA
{
	my ($sha, $length) = @_;
	$length = 10 if not defined $length;
	return substr($sha, 0, $length);
}

sub getBranchName 
{
	my $ref = shift;

	$ref =~ /.*\/(.*)/;
	return $1
}

sub parsePayload
{
	my ($payload) = @_;
	my $message;
	my %actions = (
		'opened' => 'cоздал',
		'closed' => 'закрыл',
		'synchronize' => 'подтянул',
	);

	print Dumper($payload);
	#обрабатываем следующие сообщения:
	#пулл-реквест (создание, закрытие)
	#новый комментарий 
	#новая issue(?)
	#коммит 
	if(defined ($payload->{'comment'})) {
		print_log("Comment action\n");
		my $user = $payload->{'sender'}->{'login'};
		my $comment = $payload->{'comment'}->{'body'};
		if(defined ($payload->{'issue'})) {
			#комментарий к тикету/пулл-реквесту
			my $issue = $payload->{'issue'}->{'title'};
			my $issue_number = $payload->{'issue'}->{'number'};
			my $issue_url = $payload->{'issue'}->{'html_url'};
			$message = "Пользователь $user прокомментировал задачу #$issue_number \"$issue\" ($issue_url):\n$comment";
		} elsif (defined ($payload->{'comment'}->{'commit_id'}) ) {
			#комментарий к коммиту
			my $commit_id = truncateSHA($payload->{'comment'}->{'commit_id'});
			my $commit_url = $payload->{'comment'}->{'html_url'}; 
			$message = "Пользователь $user прокомментировал коммит $commit_id ($commit_url):\n$comment";
		}
	} elsif(defined ($payload->{'pull_request'})) {
		print_log("Pull-request action\n");
		my $pull_request = $payload->{'pull_request'}; 

		my $action = $actions{$payload->{'action'}};
		$action = $payload->{'action'} if not defined $action;

		my $user = $payload->{'sender'}->{'login'};
		my $url = $pull_request->{'html_url'};
		my $diff = $pull_request->{'diff_url'};
		my $body = $pull_request->{'body'};
		my $title = $pull_request->{'title'};
		my $base = $pull_request->{'base'}->{'repo'}->{'full_name'} . ':' . $pull_request->{'base'}->{'ref'};
		my $head = $pull_request->{'head'}->{'repo'}->{'full_name'} . ':' . $pull_request->{'head'}->{'ref'};

		$message = "Пользователь $user $action пулл-реквест ($url) из $head в $base:\n$title: $body\n\nСписок изменений: $diff";

	} elsif (defined ($payload->{'commits'}) and ref($payload->{'commits'}) eq 'ARRAY') {
		print_log("Commit action\n");
		my $user = $payload->{'pusher'}->{'name'};
		my $branch = getBranchName($payload->{'ref'});
		my $repo = $payload->{'repository'}->{'url'};
		my $repo_name = $payload->{'repository'}->{'name'};
		my $current_state =  truncateSHA($payload->{'head_commit'}->{'id'}, 12);
		my $prev_state = truncateSHA($payload->{'before'}, 12);

		if(scalar(@{$payload->{'commits'}}) > 0) {
			my @push_commits;
			foreach my $commit (@{ $payload->{'commits'} }) {
				push(@push_commits, getDisplayName($commit->{'committer'}) . "\n" . truncateSHA($commit->{'id'}) . ": "	. $commit->{'message'})
			}
			$message = "В репозиторий $repo в ветку $branch пользователем $user были отправлены следующие коммиты:\n" .
			join("\n-----\n", @push_commits) . "\n========\nИзменения: $repo/compare/$prev_state...$current_state";
			

		} elsif($payload->{'after'} =~ /^0*$/ ) {
			$message = "Пользователь $user удалил ветку $branch из репозитория $repo";
		}
		print_log("Running repo update script...\n");
		system("/bin/sh -c '/home/vcs/scripts/update_repo.sh $repo_name &'");
	}

	print_log("message is $message\n");
	return $message;
}


sub processRequest 
{
	my ($httpd, $req) = @_;

	print_log('Request from ' . $req->client_host . ':' . $req->client_port . ": " . $req->method . " " . $req->url->path . "\n");
	$req->respond([200, 'OK']);

	if(defined $req->parm("payload")) {
		print $req->parm("payload");
		my $payload = from_json($req->parm("payload"), {utf8 => 1});

		my $message = parsePayload($payload);

		foreach my $jid (@developers) {
			$cl->send_message($message => $jid, undef, 'chat');
		}
	}

	$httpd->stop_request();
}


$cl->add_account ('github@welltime.ru', 'xxx', undef, undef, { resource => uniqid(), disable_ssl => "false" } );
$cl->reg_cb (
	session_ready => sub {
		my ($cl, $acc) = @_;
		$cl->send_message (
			"Github-уведомлятор запустился" => 'migalin@welltime.ru', undef, 'chat'
		);
	},
	disconnect => sub {
		my ($cl, $acc, $h, $p, $reas) = @_;
		print "disconnect ($h:$p): $reas\n";
		$j->broadcast;
	},
	contact_request_subscribe => sub {
		my ($acc, $conn, $roster, $contact, $message) = @_;
		$contact->send_subscribed();
	}, 

	error => sub {
		my ($cl, $acc, $err) = @_;
		print "ERROR: " . $err->string . "\n";
	},
	message => sub {
		my ($cl, $acc, $msg) = @_;
		print "message from: " . $msg->from . ": " . $msg->any_body . "\n";
	}
);

my %opts;

getopts("p:l:i:d", \%opts);

my $pidfile = (defined $opts{'i'})? $opts{'i'}:'/tmp/git-notifier.pid';
my $port = (defined $opts{'p'})? $opts{'p'}:9090;
my $do_daemon = defined($opts{'d'});
my $logfile =  $opts{'l'} if (defined $opts{'l'});

daemonize($logfile, $pidfile) if $do_daemon; 

$httpd =  AnyEvent::HTTPD->new (port => $port);
$httpd->reg_cb (request => \&processRequest);


$cl->start;
$cl->set_presence("available", "Нормально делай - нормально будет!", 0);
#$j->wait;
AnyEvent::Impl::Perl::loop();
